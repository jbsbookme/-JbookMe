apply plugin: 'com.android.application'

def versionCodeEnv = System.getenv('ANDROID_VERSION_CODE')
def versionNameEnv = System.getenv('ANDROID_VERSION_NAME')

def keystorePathEnv = System.getenv('ANDROID_KEYSTORE_PATH')
def keystorePasswordEnv = System.getenv('ANDROID_KEYSTORE_PASSWORD')
def keyAliasEnv = System.getenv('ANDROID_KEY_ALIAS')
def keyPasswordEnv = System.getenv('ANDROID_KEY_PASSWORD')
def hasReleaseSigning = keystorePathEnv && keystorePasswordEnv && keyAliasEnv && keyPasswordEnv

android {
    namespace "com.jbs.jbookme"
    compileSdk rootProject.ext.compileSdkVersion

    if (hasReleaseSigning) {
        signingConfigs {
            release {
                storeFile = file(keystorePathEnv)
                storePassword = keystorePasswordEnv
                keyAlias = keyAliasEnv
                keyPassword = keyPasswordEnv
            }
        }
    }

    defaultConfig {
        applicationId "com.jbs.jbookme"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCodeEnv ? versionCodeEnv.toInteger() : 2
        versionName versionNameEnv ?: "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            }
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
